plugins {
	id 'java' // Needed for libs building without xp.apps plugin
	id 'maven' // Needed for uploadArchives to work locally
	id 'maven-publish' // Provides publishToMavenLocal
	id 'com.enonic.defaults' version '2.0.0'
	id 'com.moowork.node' version '1.2.0'
}

dependencies {
	compile "com.enonic.xp:lib-admin:${xpVersion}"
	compile "com.enonic.xp:lib-content:${xpVersion}"
	compile "com.enonic.xp:lib-portal:${xpVersion}"
}

repositories {
	mavenLocal()
	jcenter()
}

node {
	version '10.15.1'
	download true
}

task wrapper(type: Wrapper) {
	gradleVersion = '4.10.3'
}

task transpile(type: NpmTask) {
	args = ['run', 'build']
	outputs.dir './build/resources/main' // processResources will delete the directory if this is not present
}

task watch(type: NpmTask) {
	args = ['run', 'watch']
}

processResources {
	dependsOn transpile
	mustRunAfter transpile
	exclude '*'
}
